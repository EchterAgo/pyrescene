@echo off
title SRR/SRS Creation Script - Working
pushd %1
IF ERRORLEVEL 1 goto :eob

set sfvfilename=
set sfvfilelist=
set rarfilename=
set buchoice=
set subchoice=

REM Setting alwaysignoresubs to YES will avoid ever storing subs in the srrs.
REM Setting alwaysstoresubs to YES will avoid being asked whether to store subs.
REM Setting alwaysbackupsrrs to YES will avoid being asked whether to create a backup srr without subs.

set alwaysignoresubs=NO
set alwaysstoresubs=YES
set alwaysbackupsrrs=YES


IF NOT EXIST CD? (

    for /f "delims==" %%d in ('dir /b *.sfv') do set sfvfilename=%%d
    call set sfvfilelist=.\%~nx1\%%sfvfilename%%
    
) ELSE (
    
    for /d %%t in (CD?) do (
      call set sfvfilelist=%%sfvfilelist%% .\%~nx1\%%t\
      pushd %%t
      for /f "delims==" %%d in ('dir /b *.sfv') do set sfvfilename=%%d
      call set sfvfilelist=%%sfvfilelist%%%%sfvfilename%%
      popd
    )

)

IF EXIST Sample (

  IF NOT EXIST CD? (

    (for /f "delims==" %%d in ('dir /b *.rar') do set rarfilename=%%d) 1>nul 2>nul
    (for /f "delims==" %%d in ('dir /b *.part01.rar') do set rarfilename=%%d) 1>nul 2>nul
    pushd Sample
    for /f "delims==" %%d in ('dir /b *.avi *.mkv') do call srs %%d -c ..\%%rarfilename%%
    popd

  ) ELSE (
    
    for /d %%t in (CD?) do (
      pushd %%t
      (for /f "delims==" %%d in ('dir /b *.rar') do set rarfilename=%%d) 1>nul 2>nul
      (for /f "delims==" %%d in ('dir /b *.part01.rar') do set rarfilename=%%d) 1>nul 2>nul
      popd
      pushd Sample
      for /f "delims==" %%d in ('dir /b *.avi *.mkv') do call srs %%d -c ..\%%t\%%rarfilename%%
      popd
    )
    
  )

)

pushd ..
call srr %%sfvfilelist%% -s .\*.nfo .\*.jpg .\Sample\*.srs .\Sample\*.jpg -p -o %~nx1.srr -i .\%~nx1
IF ERRORLEVEL 1 echo Creation of srr file failed. & goto :eob

if /i '%alwaysignoresubs%'=='YES' goto :eob

echo Checking for subtitles.
if not exist .\%~nx1\Subs echo Subtitles folder doesn't exist. & goto :eob

if /i '%alwaysstoresubs%'=='YES' goto :backupsrrq
set /p subchoice=Subtitles folder found, do you want them stored? [y/n]
if /i not '%subchoice%'=='Y' goto :eob

:backupsrrq
if /i '%alwaysbackupsrrs%'=='YES' goto :backupsrr
set /p buchoice=Create a backup srr without subtitles? [y/n]
if /i not '%buchoice%'=='Y' goto :srrstoresubs

:backupsrr
echo Creating Backup without subtitles: _%~nx1.srr
call copy %~nx1.srr _%~nx1.srr

:srrstoresubs
call srr %~nx1.srr -s .\Subs\*.rar .\Subs\*.sfv -p -i .\%~nx1

:eob
echo Exiting.
pause
title SRR/SRS Creation Script - Done