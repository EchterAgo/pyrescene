@ECHO OFF
REM This is the CHKcrc32.bat file. Version 0.1.0
if %1~==INNER~ goto INNER
REM --------------------------------------------------------------------------------
REM On all the paths to be filled in below, end with trailing slash ('\')
REM Set this the location of where the LOG FILES are going to be written
REM  i.e.  "c:\" ... or ... "d:\tools\" ... or ... c:\windows\system32\"
SET zzzLOGFILEloc=v:\
REM Set this to the location of where the fsum CRC check utility exists
SET ZZZfsum_loc=D:\USER\dosutils\
REM --------------------------------------------------------------------------------
 
 
REM -------------------------------------------------------------
REM - Get full path and name of the base batch file name we are running (%0)
FOR %%T in (%0) DO SET @Batchname=%%~fT
REM Create the Main and error log file names
REM   Create a dynamic date-time string to be appended to the log file names
set @@dt=%DATE%:%TIME%
FOR /F "tokens=2-7 delims=/:., " %%A in ("%@@dt%") do SET @@result=.%%A.%%B.%%C-%%D.%%E.%%F
SET @ErrLogFile=%zzzLOGFILEloc%CHKcrc32.ERRLOG%@@result%.txt
SET @LogFile=%zzzLOGFILEloc%CHKcrc32.LOG%@@result%.txt
ECHO Main Logfile: %@LogFile%
ECHO  ERR Logfile: %@ErrLogFile%
REM This is the main loop entry point
FOR /D %%T in (*.*) do call %@Batchname% INNER "%%T"
REM Cleanup
FOR %%T in (zzzLOGFILEloc ZZZfsum_loc @Batchname @LogFile @ErrLogFilep @@src @@src2) DO SET %%T=
goto alldone
 
 
:INNER
cd %2
REM Clear trigger flag now
SET @@src=
REM Look for a trigger condition
FOR %%T IN (*.sfv) do SET @@src=%%T
IF NOT ~"%@@src%"==~"" goto DoTest
REM Not Triggered, so recurse deeper yet!
FOR /D %%T in (*.*) do call %@Batchname% INNER "%%T"
goto DOcdBK
:DoTest
ECHO Checking %@@src%
%ZZZfsum_loc%fsum.exe -c -crc32 %@@src%
if ERRORLEVEL 1 GOTO WasErr
ECHO [OK] %@@src% (DIR %CD%)>>%@LogFile%
goto DOcdBK
:WasErr
ECHO [FAILED] %@@src% (DIR "%CD%")>>%@ErrLogFile%
:DOcdBK
cd ..
:alldone